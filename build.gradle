buildscript {
    ext {
        javaVersion = '1.8'
        kotlinVersion = '1.2.60'

        // see https://www.jetbrains.com/intellij-repository/releases/
        intellijVersion = '182.3569.1'

        // see https://plugins.jetbrains.com/idea/plugin/6884-handlebars-mustache
        handlebarsPluginVersion = '182.3458.1'
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.3.2"
    id "com.zoltu.git-versioning" version "2.0.28"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = javaVersion

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.assertj:assertj-core:3.10.0"
}

intellij {
    type = 'IU'
    version = intellijVersion
    downloadSources !System.getenv().containsKey('CI')

    pluginName = 'Ember.js'
    plugins = ['JavaScriptLanguage', 'CSS', 'yaml', "com.dmarcotte.handlebars:$handlebarsPluginVersion"]

    sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"

    publishPlugin {
        username "$JETBRAINS_USERNAME"
        password "$JETBRAINS_PASSWORD"
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.7'
    distributionType = Wrapper.DistributionType.ALL
}
