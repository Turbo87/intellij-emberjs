buildscript {
    ext {
        javaVersion = '11'
        kotlinVersion = '1.5.10'

        // see https://www.jetbrains.com/intellij-repository/releases/
        // and https://www.jetbrains.com/intellij-repository/snapshots/
        intellijVersion = '2021.1'

        // see https://plugins.jetbrains.com/plugin/6884-handlebars-mustache/versions
        handlebarsPluginVersion = '211.6693.44'
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.7.2"
    id "com.zoltu.git-versioning" version "2.0.28"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = javaVersion

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testCompile "org.assertj:assertj-core:3.20.2"
}

intellij {
    type = 'IU'
    version = intellijVersion
    downloadSources !System.getenv().containsKey('CI')

    pluginName = 'Ember.js'
    plugins = [
        'JavaScriptLanguage',
        'CSS',
        'platform-images',
        'yaml',
        "com.dmarcotte.handlebars:$handlebarsPluginVersion",
    ]

    sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"

    publishPlugin {
        token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
    }
}

runIdea {
    jvmArgs = [ '-Xmx1g' ]
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '5.5.1'
    distributionType = Wrapper.DistributionType.ALL
}
