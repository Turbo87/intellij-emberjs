buildscript {
    ext {
        javaVersion = '1.7'
        kotlinVersion = '1.1.2'

        // see https://www.jetbrains.com/intellij-repository/releases/
        intellijVersion = '173.2099.14'

        // see https://plugins.jetbrains.com/idea/plugin/6884-handlebars-mustache
        handlebarsPluginVersion = '173.2099.6'
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.2.12"
    id "com.zoltu.git-versioning" version "2.0.28"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = javaVersion

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-runtime:$kotlinVersion"
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    testCompile "org.assertj:assertj-core:2.2.0"
}

intellij {
    type = 'IU'
    version = intellijVersion
    downloadSources !System.getenv().containsKey('CI')

    pluginName = 'Ember.js'
    plugins = ['JavaScriptLanguage', 'CSS', 'yaml', "com.dmarcotte.handlebars:$handlebarsPluginVersion"]

    sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"

    publishPlugin {
        username "$JETBRAINS_USERNAME"
        password "$JETBRAINS_PASSWORD"
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.2.1'
    distributionType = Wrapper.DistributionType.ALL
}
