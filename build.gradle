buildscript {
    ext {
        javaVersion = '1.8'
        kotlinVersion = '1.3.72'

        // see https://www.jetbrains.com/intellij-repository/releases/
        // and https://www.jetbrains.com/intellij-repository/snapshots/
        intellijVersion = '202.7660.26'

        // see https://plugins.jetbrains.com/plugin/6884-handlebars-mustache/versions
        handlebarsPluginVersion = '202.6397.21'
    }

    repositories {
        mavenCentral()
        maven { url "https://jitpack.io" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
    }
}

plugins {
    id "org.jetbrains.intellij" version "0.5.0"
    id "com.zoltu.git-versioning" version "2.0.28"
}

apply plugin: 'org.jetbrains.intellij'
apply plugin: 'java'
apply plugin: 'kotlin'

sourceCompatibility = javaVersion

repositories {
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
    testCompile "org.assertj:assertj-core:3.18.0"
}

intellij {
    type = 'IU'
    version = intellijVersion
    downloadSources !System.getenv().containsKey('CI')

    pluginName = 'EmberExperimental.js'
    plugins = [
        'JavaScriptLanguage',
        'CSS',
        'platform-images',
        'yaml',
        "com.dmarcotte.handlebars:$handlebarsPluginVersion",
    ]

    sandboxDirectory = project.rootDir.canonicalPath + "/.sandbox"

    publishPlugin {
        token = System.getenv("ORG_GRADLE_PROJECT_intellijPublishToken")
    }
    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }
}


test {
    testLogging {
        exceptionFormat = 'full'
    }
}

wrapper {
    gradleVersion = '5.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

